/*
 * Copyright 2015 Norbert Potocki (norbert.potocki@nort.pl)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
// ----------- Build script configuration -----------

buildscript {

    ext {
        artifactGroup = "pl.nort"
        artifactName = "config"
        artifactVersion = "1.0-SNAPSHOT"
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

// ----------- Plugins -----------

apply plugin: "java"
apply plugin: "idea"
apply plugin: "jacoco"
apply plugin: 'maven'
apply plugin: 'signing'

// ----------- Repositories -----------

repositories {
    mavenLocal()
    mavenCentral()
}

// ----------- External module dependencies -----------

dependencies {
    compile group: "com.google.guava", name: "guava", version: "18.0"

    // Git
    compile group: "org.eclipse.jgit", name: "org.eclipse.jgit", version: "3.7.1.201504261725-r"

    // Logging
    compile group: "org.slf4j", name: "slf4j-api", version: "1.7.9"

    // Code quality
    compile group: "com.google.code.findbugs", name: "findbugs", version: "3.0.1"

    testCompile group: "junit", name: "junit", version: "4.11"
    testCompile group: "org.easytesting", name: "fest-assert-core", version: "2.0M10"
    testCompile group: "org.mockito", name: "mockito-core", version: "1.9.5"
}

// ----------- EXTRA tasks -----------

// Generate Gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = "2.3"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// ----------- Task configurations -----------
defaultTasks "clean", "build"

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

jar {
    baseName = "${artifactName}"
    version = "${artifactVersion}"
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled false
    }
}

group = "${artifactGroup}"
archivesBaseName = "${artifactName}"
version = "${artifactVersion}"

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Nort Config'
                packaging 'jar'
                description 'Configuration management for distributed Java applications made easy.'
                url 'https://github.com/nort/config'

                scm {
                    url 'git@github.com:nort/config.git'
                    connection 'scm:git:git@github.com:nort/config.git'
                    developerConnection 'scm:git:git@github.com:nort/config.git'
                }


                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'norbertpotocki'
                        name 'Norbert Potocki'
                        email 'norbert.potocki@nort.pl'
                    }
                }
            }
        }
    }
}


signing {
    sign configurations.archives
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        Console console = System.console()
        console.printf "\n\nWe have to sign and upload artifacts to Maven Central Repository" +
                "\n\nPlease enter your credentials.\n\n"

        def signingPassword = console.readPassword("PGP Private Key Password: ")
        def ossrhPassword = console.readPassword("Maven Central Password: ")

        allprojects { ext."signing.password" = signingPassword }
        allprojects { ext."ossrhPassword" = ossrhPassword }


        console.printf "\nThanks.\n\n"
    }
}